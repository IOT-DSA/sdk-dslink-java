apply plugin: 'signing'
apply plugin: 'maven'

group = 'org.iot-dsa'

ext {
    if (!project.hasProperty("ossrhUsername"))
        ossrhUsername = ""
    if (!project.hasProperty("ossrhPassword"))
        ossrhPassword = ""
}

repositories {
    mavenLocal()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                packaging = 'jar'
                url = 'http://iot-dsa.org'

                scm {
                    connection = 'scm:git:https://github.com/IOT-DSA/sdk-dslink-java.git'
                    developerConnection = 'scm:git:git@github.com:IOT-DSA/sdk-dslink-java.git'
                    url = 'https://github.com/IOT-DSA/sdk-dslink-java'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'samrg472'
                        name = 'Samuel Grenier'
                        email = 'samrg472@gmail.com'
                    }
                }
            }
        }
    }
}
